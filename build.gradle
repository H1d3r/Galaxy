// from https://github.com/grpc/grpc-java/blob/master/examples/build.gradle

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.m2sec'
version = '2.2.6'
def grpcVersion = '1.54.1'
def protobufVersion = '3.24.0'
def protocVersion = protobufVersion

repositories {
//    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/public' }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    // annotation
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    // burp
    compileOnly 'net.portswigger.burp.extensions:montoya-api:2023.12.1'
    // yaml
    implementation 'org.yaml:snakeyaml:2.2'
    // grpc
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    // crypto
    compileOnly 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    // template
    implementation 'org.apache.commons:commons-text:1.12.0'
    // log
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    // code text area
    implementation 'com.fifesoft:rsyntaxtextarea:3.4.1'
    implementation 'com.fifesoft:autocomplete:3.3.1'
    // cross-language
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'
    implementation 'org.python:jython-standalone:2.7.3'
    // test
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'net.portswigger.burp.extensions:montoya-api:2023.12.1'
    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    testImplementation 'org.projectlombok:lombok:1.18.32'
    testImplementation 'com.fifesoft:rsyntaxtextarea:3.4.1'
    testImplementation 'org.python:jython-standalone:2.7.3'
    testImplementation 'org.openjdk.nashorn:nashorn-core:15.4'
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    // 写入版本
    def versionFile = file("${projectDir}/src/main/resources/version.txt")
    versionFile.text = version
    mergeServiceFiles()
}